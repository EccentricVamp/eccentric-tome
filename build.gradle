plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
}

java.toolchain.languageVersion = JavaLanguageVersion.of java_version

version = "${minecraft_version}-${mod_version}"
group = mod_group
base.archivesName = mod_id

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }

            properties = [
                'mixin.env.remapRefMap'        : 'true',
                'mixin.env.refMapRemappingFile': project.file('build/createSrgToMcp/output.srg')
            ]
        }
        client {
            taskName 'Client'
        }

        server {
            taskName 'Server'
        }
    }
}

repositories {
    maven {
        url 'https://cursemaven.com'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    runtimeOnly fg.deobf("curse.maven:jei-238222:5846810")
    runtimeOnly fg.deobf("curse.maven:botania-225643:5594997")
    runtimeOnly fg.deobf("curse.maven:mystical-agriculture-246640:5777070")
    runtimeOnly fg.deobf("curse.maven:cucumber-272335:5714571")
    runtimeOnly fg.deobf("curse.maven:curios-309927:5843594")
    runtimeOnly fg.deobf("curse.maven:patchouli-306770:4966125")
}

processResources {
    var replaceProperties = [
            minecraft_version       : minecraft_version,
            minecraft_version_range : minecraft_version_range,
            forge_version           : forge_version,
            forge_version_range     : forge_version_range,
            loader_version_range    : loader_version_range,
            mod_id                  : mod_id,
            mod_name                : mod_name,
            mod_license             : mod_license,
            mod_version             : mod_version,
            mod_author              : mod_author,
            mod_description         : mod_description,
            pack_format             : pack_format,
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

jar.manifest.attributes = [
    'Specification-Title'     : mod_id,
    'Specification-Vendor'    : mod_author,
    'Specification-Version'   : '1',
    'Implementation-Title'    : project.name,
    'Implementation-Version'  : mod_version,
    'Implementation-Vendor'   : mod_author,
    'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ'),
]

jar.finalizedBy 'reobfJar'

curseforge {
    apiKey = System.getenv().CURSEFORGE_KEY ?: 'none'
    project {
        id = curseforge_project_id
        releaseType = curseforge_release_type
        addGameVersion 'Forge'
        addGameVersion minecraft_version
        addGameVersion "Java ${java_version}"
        mainArtifact jar
    }
}

modrinth {
    token = System.getenv().MODRINTH_TOKEN ?: 'none'
    projectId = modrinth_project_id
    versionType = modrinth_release_type
    gameVersions = [minecraft_version]
    uploadFile = jar
}